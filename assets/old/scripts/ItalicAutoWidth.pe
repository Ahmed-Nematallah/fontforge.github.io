#!/usr/bin/pfaedit

/* This script for PfaEdit was created by Maxim Iorsh in 2003. It is public
domain. You can use it in any way and for any purpose.

I noticed once that AutoWidth performed on italic font looks better if a
font is deitalicized prior to the procedure. This script implements it. It
can be invoked from the command line with a font file name as an argument,
or added to the File->Script Menu for convenience.

Remark: For *serifed* fonts it is recommended to decrement LBearing by the
length of serif after the AutoWidth.

This script was developed and tested with build 030310.
*/

// Try to determine whether there is an active font
if ($firstfont == "")
	// No font is currently loaded
	// Try to get font name from argument
	if ($argc == 1)
		Error("Exiting - no font provided!");
	else
		myFont = $1;	// font name = first argument
	endif
	
	// Remember that the font was loaded from file, and it should be saved
	// after the work is done.
	needSave = 1;
else
	myFont = $curfont;	// font name = current active font
	needSave = 0;
endif

// Try to open the font. If this is the current font (obtained from $curfont),
// pfaedit will do nothing. If this is a file name obtained from command line,
// a failure may occur due to incorrect font format or missing file. Such
// failure will cause the script to abort.
Open(myFont);

// We want to determine whether there is any current selection. Having no
// straightforward way to do it, we will run 'foreach' and if it executes 
// even once - we have a non-empty selection. This is ugly and inefficient,
// but we don't pretend to be good programmers.
isSel = 0;	// assume nothing selected and set the flag FALSE
foreach
	isSel = 1;	// set the flag TRUE
endloop

// If nothing was selected, select everything
if ( isSel == 0 )
	SelectAll();
endif

// Now perform the *main* operation on the selection

answidth = AskUser("Please set the desired spacing between characters:");
width = Strtol(answidth);
if ( width > 0 )
	Skew($italicangle,0,0);		// unskew the font
	AutoWidth(width);		// perform AutoWidth with desired width
	Skew(0-$italicangle,0,0);	// skew the font back
else
	Error("Exiting - invalid width specified!");
endif

// If the font was loaded from file (script was called from command line),
// we must save it. If the font was being edited (script was invoked
// interactively from menu), we must not save it.
if ( ($fontchanged == 1) && (needSave == 1) )
	Save();
endif
